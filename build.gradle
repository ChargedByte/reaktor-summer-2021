//file:noinspection GradlePackageUpdate

plugins {
    id('org.springframework.boot') version('2.5.4')
    id('io.spring.dependency-management') version('1.0.11.RELEASE')
    id('org.barfuin.gradle.taskinfo') version('1.0.5')
    id('java')
}

group('dev.chargedbyte')
version('1.0.0')

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('com.google.code.findbugs:jsr305:3.0.2')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.data:spring-data-keyvalue')

    compileOnly('org.projectlombok:lombok')

    developmentOnly('org.springframework.boot:spring-boot-devtools')

    annotationProcessor('org.projectlombok:lombok')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

test {
    useJUnitPlatform()
}

task copyAppToBuildResources(type: Copy) {
    dependsOn(':app:build')

    from(project(':app').buildDir)
    into("${buildDir}/resources/main/static")

    include('**/*')
}

bootJar.dependsOn(copyAppToBuildResources)

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter(clean)
